# Copyright (c) 2014 The Chromium Embedded Framework Authors. All rights
# reserved. Use of this source code is governed by a BSD-style license that
# can be found in the LICENSE file.

#
# Source files.
#

# app sources.
set(APP_MAIN_SRCS
  src/app.cpp
  src/stdafx.h
)
set(APP_SRCS ${APP_MAIN_SRCS})
set(APP_SRCS_WINDOWS
#  app.exe.manifest
  res/windows/Zephyros_SampleApp.rc
#  resource.h
  src/stdafx.cpp
)
APPEND_PLATFORM_SOURCES(APP_SRCS)
source_group(app FILES ${APP_SRCS})

set(APP_RES_SRCS_WINDOWS
  res/windows/Zephyros_SampleApp.ico
  res/windows/small.ico
)
APPEND_PLATFORM_SOURCES(APP_RES_SRCS)
source_group(app\\\\res FILES ${APP_RES_SRCS})

set(APP_SRCS
  ${APP_SRCS}
  ${APP_RES_SRCS}
)

# app helper sources.
#set(APP_HELPER_SRCS_MACOSX
#  src/app.cpp
#  src/stdafx.h
#)
#APPEND_PLATFORM_SOURCES(APP_HELPER_SRCS)
#source_group(app FILES ${APP_HELPER_SRCS})

# app resources.
set(APP_RESOURCES_MAC_SRCS_MACOSX
  res/mac/Info-CEF.plist
#  mac/app.icns
)
APPEND_PLATFORM_SOURCES(APP_RESOURCES_MAC_SRCS)
source_group(app\\\\mac FILES ${APP_RESOURCES_MAC_SRCS})

set(APP_RESOURCES_MAC_BASE_LPROJ_SRCS_MACOSX
  res/mac/Base.lproj/MainMenu.xib
)
APPEND_PLATFORM_SOURCES(APP_RESOURCES_MAC_BASE_LPROJ_SRCS)
source_group(app\\\\mac\\\\Base.lproj FILES ${APP_RESOURCES_MAC_BASE_LPROJ_SRCS})

set(APP_RESOURCES_MAC_ENGLISH_LPROJ_SRCS_MACOSX
  res/mac/en.lproj/Localizable.strings
)
APPEND_PLATFORM_SOURCES(APP_RESOURCES_MAC_ENGLISH_LPROJ_SRCS)
source_group(app\\\\mac\\\\English.lproj FILES ${APP_RESOURCES_MAC_ENGLISH_LPROJ_SRCS})

set(APP_RESOURCES_SRCS
  ${APP_RESOURCES_MAC_SRCS}
  ${APP_RESOURCES_MAC_BASE_LPROJ_SRCS}
  ${APP_RESOURCES_MAC_ENGLISH_LPROJ_SRCS}
)


#
# Shared configuration.
#

# Target executable names.
set(CEF_TARGET "App")
if(OS_MACOSX)
  set(CEF_HELPER_TARGET "App Helper")
endif()

# Logical target used to link the libcef library.
ADD_LOGICAL_TARGET("libcef_lib" "${CEF_LIB_DEBUG}" "${CEF_LIB_RELEASE}")

# Determine the target output directory.
SET_CEF_TARGET_OUT_DIR()


#
# Linux configuration.
#

if(OS_LINUX)
  # Executable target.
  add_executable(${CEF_TARGET} ${APP_SRCS})
  add_dependencies(${CEF_TARGET} libcef_dll_wrapper)
  target_link_libraries(${CEF_TARGET} libcef_lib libcef_dll_wrapper ${CEF_STANDARD_LIBS})

  # Set rpath so that libraries can be placed next to the executable.
  set_target_properties(${CEF_TARGET} PROPERTIES INSTALL_RPATH "$ORIGIN")
  set_target_properties(${CEF_TARGET} PROPERTIES BUILD_WITH_INSTALL_RPATH TRUE)
  set_target_properties(${CEF_TARGET} PROPERTIES RUNTIME_OUTPUT_DIRECTORY ${CEF_TARGET_OUT_DIR})

  # Copy binary and resource files to the target output directory.
  COPY_FILES("${CEF_TARGET}" "${CEF_BINARY_FILES}" "${CEF_BINARY_DIR}" "${CEF_TARGET_OUT_DIR}")
  COPY_FILES("${CEF_TARGET}" "${CEF_RESOURCE_FILES}" "${CEF_RESOURCE_DIR}" "${CEF_TARGET_OUT_DIR}")

  # Set SUID permissions on the chrome-sandbox target.
#  SET_LINUX_SUID_PERMISSIONS("${CEF_TARGET}" "${CEF_TARGET_OUT_DIR}/chrome-sandbox")
endif()


#
# Mac OS X configuration.
#

if(OS_MACOSX)
  # Output paths for the app bundles.
  set(CEF_APP "${CEF_TARGET_OUT_DIR}/${CEF_TARGET}.app")
  set(CEF_HELPER_APP "${CEF_TARGET_OUT_DIR}/${CEF_HELPER_TARGET}.app")

  # Variable referenced from Info.plist files.
  set(PRODUCT_NAME "${CEF_TARGET}")

  # Helper executable target.
#  add_executable(${CEF_HELPER_TARGET} MACOSX_BUNDLE ${APP_HELPER_SRCS})
  add_executable(${CEF_HELPER_TARGET} MACOSX_BUNDLE ${APP_MAIN_SRCS})
  add_dependencies(${CEF_HELPER_TARGET} libcef_dll_wrapper)
  target_link_libraries(${CEF_HELPER_TARGET} libcef_lib libcef_dll_wrapper ${CEF_STANDARD_LIBS})
  set_target_properties(${CEF_HELPER_TARGET} PROPERTIES
    MACOSX_BUNDLE_INFO_PLIST ${CMAKE_CURRENT_SOURCE_DIR}/res/mac/Info-CEF-Helper.plist
  )

  # Fix the framework link in the helper executable.
  FIX_MACOSX_HELPER_FRAMEWORK_LINK(${CEF_HELPER_TARGET} ${CEF_HELPER_APP})

  # Main executable target.
  add_executable(${CEF_TARGET} MACOSX_BUNDLE ${APP_RESOURCES_SRCS} ${APP_SRCS})
  add_dependencies(${CEF_TARGET} libcef_dll_wrapper "${CEF_HELPER_TARGET}")
  target_link_libraries(${CEF_TARGET} libcef_lib libcef_dll_wrapper ${CEF_STANDARD_LIBS})
  set_target_properties(${CEF_TARGET} PROPERTIES
    RESOURCE "${APP_RESOURCES_SRCS}"
    MACOSX_BUNDLE_INFO_PLIST ${CMAKE_CURRENT_SOURCE_DIR}/res/mac/Info-CEF.plist
  )

  # Copy files into the main app bundle.
  add_custom_command(
    TARGET ${CEF_TARGET}
    POST_BUILD
    # Copy the helper app bundle into the Frameworks directory.
    COMMAND ${CMAKE_COMMAND} -E copy_directory
            "${CEF_HELPER_APP}"
            "${CEF_APP}/Contents/Frameworks/${CEF_HELPER_TARGET}.app"
    # Copy the CEF framework into the Frameworks directory.
    COMMAND ${CMAKE_COMMAND} -E copy_directory
            "${CEF_BINARY_DIR}/Chromium Embedded Framework.framework"
            "${CEF_APP}/Contents/Frameworks/Chromium Embedded Framework.framework"
    VERBATIM
  )

  # Fix the framework link in the main executable.
  FIX_MACOSX_MAIN_FRAMEWORK_LINK(${CEF_TARGET} ${CEF_APP})

  # Make the other helper app bundles.
  MAKE_MACOSX_HELPERS(${CEF_TARGET} ${CEF_APP})

  if(NOT ${CMAKE_GENERATOR} STREQUAL "Xcode")
    # Manually process and copy over resource files.
    # The Xcode generator handles this via the set_target_properties RESOURCE directive.
    set(PREFIXES "mac/")  # Remove these prefixes from input file paths.
    COPY_MACOSX_RESOURCES("${APP_RESOURCES_SRCS}" "${PREFIXES}" "${CEF_TARGET}" "${CMAKE_CURRENT_SOURCE_DIR}" "${CEF_APP}")
  endif()
endif()


#
# Windows configuration.
#

if(OS_WINDOWS)
  # Executable target.
  add_executable(${CEF_TARGET} WIN32 ${APP_SRCS})
  add_dependencies(${CEF_TARGET} libcef_dll_wrapper)
  target_link_libraries(${CEF_TARGET} libcef_lib libcef_dll_wrapper ${CEF_STANDARD_LIBS})

  # Add the custom manifest files to the executable.
  ADD_WINDOWS_MANIFEST("${CMAKE_CURRENT_SOURCE_DIR}" "${CEF_TARGET}")

  # Copy binary and resource files to the target output directory.
  COPY_FILES("${CEF_TARGET}" "${CEF_BINARY_FILES}" "${CEF_BINARY_DIR}" "${CEF_TARGET_OUT_DIR}")
  COPY_FILES("${CEF_TARGET}" "${CEF_RESOURCE_FILES}" "${CEF_RESOURCE_DIR}" "${CEF_TARGET_OUT_DIR}")
endif()
